plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.1.20'
}

android {
    namespace 'com.rnkotlincompose.perse'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        consumerProguardFiles 'consumer-rules.pro'
        buildConfigField 'String', 'API_BASE_URL', '"https://gorest.co.in/public/v2/"'
        def localProps = new Properties()
        def lpFile = project.rootProject.file('local.properties')
        if (lpFile.exists()) {
            localProps.load(new FileInputStream(lpFile))
        }
        def gorestToken = localProps.getProperty('GOREST_TOKEN', System.getenv('GOREST_TOKEN') ?: '')
        buildConfigField 'String', 'GOREST_TOKEN', '"' + gorestToken.replace('"','\\"') + '"'
        buildConfigField 'String', 'TOKEN_ROTATE_URL', '""'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    buildFeatures {
        buildConfig true
        compose true
    }
}

dependencies {
    implementation project(':core')
    implementation project(':coreui')

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation platform('androidx.compose:compose-bom:2024.10.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.compose.material3:material3:1.3.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'

    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.15.2'

    compileOnly("com.facebook.react:react-android")
    // Secure storage for tokens
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
}
